#version 430 core

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct ShaderConfig {
    int agentsCount;
    int width;
    int height;
    float dt;
    float t;
};

struct Agent
{
    vec2 position;
    float angle;
    uint type;
    float energy;
    float age;
    int state;
    int pad;
};

layout(std430, binding = 0) buffer ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer AgentsBuffer {
    Agent agents[];
};

layout(rgba32f, binding = 2) uniform image2D inPlants;
layout(rgba32f, binding = 3) uniform image2D inPray;
layout(rgba32f, binding = 4) uniform image2D inPred;

layout(rgba32f, binding = 5) uniform image2D outPlants;
layout(rgba32f, binding = 6) uniform image2D outPray;
layout(rgba32f, binding = 7) uniform image2D outPred;

void torus(inout vec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

void itorus(inout ivec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return state / 4294967295.0;
}

float get_random_float_01(Agent agent, uint seed)
{
     uint random = hash(uint(agent.position.y * config.width + agent.position.x) + hash(gl_GlobalInvocationID.x + int(config.t) * seed));
     float random01 = scaleToRange01(random);
     return random01;
}

void update_one(uint idx)
{
    Agent agent = agents[idx];

    if (agent.type > 0)
    {
        float random01 = get_random_float_01(agent, 100037);
        float velocity = 0.1;
        agent.angle += (0.5-random01) * 0.1;
        agent.position += vec2(velocity*cos(agent.angle),velocity*sin(agent.angle));
    }
    
    torus(agent.position);
    agents[idx] = agent;

    ivec2 pixelPos = ivec2(uint(agent.position.x), uint(agent.position.y));
    if (agent.type == 0)
        imageStore(outPlants, pixelPos, vec4(1,1,1,1)); 
    else if (agent.type == 1)
        imageStore(outPray, pixelPos, vec4(1,1,1,1));
    else if (agent.type == 2)
        imageStore(outPred, pixelPos, vec4(1,1,1,1));
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if (idx >=0 && idx < config.agentsCount) 
    {
        update_one(idx);
    }
}