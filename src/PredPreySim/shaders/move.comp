#version 430 core

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct ShaderConfig {
    int agentsCount;
    int width;
    int height;
    float dt;
    float t;
};

struct Agent
{
    vec2 position;
    float angle;
    uint type;
    float energy;
    int age;
    int state;
    int nnOffset;
    int meals;
    int deaths;
    float energySpent;
    int flag;
};

layout(std430, binding = 0) buffer ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer AgentsBuffer {
    Agent agents[];
};

layout(rgba32f, binding = 2) uniform image2D inGreen;
layout(rgba32f, binding = 3) uniform image2D inBlue;
layout(rgba32f, binding = 4) uniform image2D inRed;

layout(std430, binding = 5) buffer NetworkBuffer {
    float network[];
};

const float M_PI        = 3.14159265358979323846;

void torus(inout vec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

void itorus(inout ivec2 pos)
{
    if (pos.x > config.width)
        pos.x -= config.width;
    else if (pos.x < 0)
        pos.x += config.width;

    if (pos.y > config.height)
        pos.y -= config.height;
    else if (pos.y < 0)
        pos.y += config.height;
}

uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return state / 4294967295.0;
}

float get_random_float_01(Agent agent, uint seed)
{
     uint random = hash(uint(agent.position.y * config.width + agent.position.x) + hash(gl_GlobalInvocationID.x + int(config.t) * seed));
     float random01 = scaleToRange01(random);
     return random01;
}

vec3 sense(Agent agent, float sensorAngle, float sensorDist, int sensorSize)
{
    float sensingAngle = agent.angle + sensorAngle;
    vec2 sensingDir = vec2(cos(sensingAngle),sin(sensingAngle));
    vec2 sensingCenter = agent.position + sensingDir * sensorDist;
    ivec2 center = ivec2(int(sensingCenter.x),int(sensingCenter.y));
    vec3 sum = vec3(0,0,0);
    for(int offsetX=-sensorSize; offsetX <= sensorSize; offsetX++)
        for(int offsetY=-sensorSize; offsetY <= sensorSize; offsetY++)
        {
            ivec2 sensePixel = center + ivec2(offsetX, offsetY);
            itorus(sensePixel);
            sum.r += imageLoad(inRed, sensePixel).r;
            sum.g += imageLoad(inGreen, sensePixel).r;
            sum.b += imageLoad(inBlue, sensePixel).r;
        }

    uint r = 2*sensorSize+1;
    return sum / (r*r);
}

float[15] prepare_input(Agent agent)
{
    float inp[15];
    float angles[5] = float[] ( -0.15*M_PI , 0, +0.15*M_PI, -0.75*M_PI, +0.75*M_PI );
    for(int a=0; a<5; a++)
    {
        vec3 sensor = sense(agent, angles[a], 3, 1);
        for(int c=0; c<3; c++)
            inp[a*3+c] = sensor[c];
    }

    return inp;
}

float activate(float x)
{
    return tanh(x);
}

float[2] evaluate_network(float inp[15], int offset)
{
    // Hidden layer
    float hidden[8];
    for (int h = 0; h < 8; ++h)
    {
        float sum = network[offset+120+h];
        for (int i = 0; i < 15; ++i)
            sum += inp[i] * network[offset+h*15+i];

        hidden[h] = activate(sum);
    }

    // Output layer
    float result[2];
    for (int o = 0; o < 2; ++o)
    {
        float sum = network[offset+144+o];
        for (int h = 0; h < 8; ++h)
            sum += hidden[h] * network[offset+128+o*8+h];

        result[o] = activate(sum);
    }


    return result;
}

void update_one(uint idx)
{
    Agent agent = agents[idx];

    agent.age += 1;
    if (agent.type == 0 && agent.state == 1 && agent.age > 0)
        agent.state = 0;
       
    agents[idx] = agent;
    if (agent.state == 1)
        return;

    if (agent.type > 0)
    {
        float[] inp = prepare_input(agent);
        float[] res = evaluate_network(inp, agent.nnOffset);

        agent.angle += 0.2 * res[0];
        agent.angle = mod(agent.angle + M_PI, 2*M_PI) - M_PI;
        float velocity = 0.5 * (res[1] + 1);
        agent.position += vec2(velocity*cos(agent.angle),velocity*sin(agent.angle));

        float energyConsumed = (0.6 * velocity * velocity + 0.05);

        agent.energy -= energyConsumed;
        agent.energySpent += energyConsumed;

    }
    
    torus(agent.position);
    agents[idx] = agent;

}

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if (idx >=0 && idx < config.agentsCount) 
    {
        update_one(idx);
    }
}